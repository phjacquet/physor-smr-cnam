*----
* GODIVA
*----

LINKED_LIST
  LIBMIX   LIBMIXS
  GCELL    
  TRACK SYS FLUX
  EDC
  DATABASE DBC
  ;
MODULE
  GEO: LIB: 
  SHI: USS: 
  SYBILT: EXCELT: SNT: ASM: FLU: EDI:
  DELETE: END:
  PSP: UTL: COMPO: EVO:
  ;
*----
*  Microscopic cross sections from JEF2 draglib file
*----
LIBMIX := LIB: :: EDIT 0
     NMIX 1
     CTRA WIMS
     SUBG
     ANIS 2
     DEPL LIB: DRAGON FIL: DLIB_J2_361
     MIXS LIB: DRAGON FIL: DLIB_J2_361
  MIX 1 300.0
     U234 = 'U234'   4.9184E-04  1
     U235 = 'U235'   9.6028E-03  1
     U238 = 'U238'   3.7443E-02  1
;

*----
*  Spherical geometry
*----
GCELL := GEO: :: SPHERE  1
           R+ REFL
           MIX  1
           RADIUS 0.0 21.55
           SPLITR 15
           ;
*----
*  Analysis of geometry
*----
TRACK := SNT: GCELL ::
  TITLE 'Fuel'
  EDIT 1 MAXR 30 
  DIAM 1 SN 16
  SCAT 2
  QUAB 7
;
*----
*  Self Shielding
*----
LIBMIXS := USS: LIBMIX TRACK :: MAXST 200 EDIT 5 ARM ;

*----
*  SN problem definition
*----
SYS := ASM: LIBMIXS TRACK :: ARM ;

*----
*  Problem solution
*----
FLUX := FLU: SYS LIBMIXS TRACK  ::
     TYPE K
     EXTE 100 1E-4 THER 5 1E-4 ;

*----
*  Solution edition and processing
*----
EDC := EDI: LIBMIXS TRACK FLUX ::
  EDIT 0
  MERG COMP COND 7.4080E06 3.6790E06 6.7210E05 1.1110E05 1.9310E04 
          3.3550E03 1.5850E03 7.4850E02 2.7540E02 1.3010E02 
          6.1440E01 2.9000E01 1.3700E01 8.3200E00 5.0400E00 
          2.3800E00 1.2900E00 6.5000E-01 3.5000E-01 2.0000E-01 
          1.2000E-01 8.0000E-02 5.0000E-02 2.0000E-02 
          1.0000E-02
  SAVE ON 'FCCg' ;


END: ;
